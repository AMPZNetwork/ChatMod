plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    //id 'org.comroid.codegen.spigot'
}

ext {
    artifactName = 'chatmod-core'
    projectDescription = 'Minecraft cross-server-chat Mod'
    userbase = 'AMPZNetwork'
}

group = 'com.ampznetwork.chatmod'
sourceCompatibility = 21
targetCompatibility = 21

apply from: "$parent.projectDir/gradle/publishing.gradle"

shadowJar { setProperty("zip64",true) }

repositories {
    maven { url 'https://maven.comroid.org' }
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api project("$prefix:api")

    // serialization
    implementation 'net.kyori:adventure-api:4.+'
    implementation 'net.kyori:adventure-text-serializer-gson:4.+'
    implementation 'net.kyori:adventure-text-serializer-plain:4.+'
    implementation 'com.google.code.gson:gson:2.11.0'

    // database
    api 'org.hibernate:hibernate-core:5.6.0.Final'
    api 'org.jboss:jandex:2.4.5.Final'
    api 'com.zaxxer:HikariCP:4.0.3'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client:3.4.0'
    runtimeOnly 'com.mysql:mysql-connector-j:8.4.0'
    runtimeOnly 'com.h2database:h2:2.2.224'

    // logger
    compileOnly 'org.apache.logging.log4j:log4j-core:2.+'

    // utility
    compileOnly 'org.projectlombok:lombok:+'
    annotationProcessor 'org.projectlombok:lombok:+'

    // tests
    testImplementation 'net.kyori:adventure-text-serializer-gson:4.14.0'
}

task simplifyArtifact(type: Copy) {
    // api module should be slim, and only contain chatMod's own api
    dependsOn test, jar
    from jar.outputs.files.singleFile
    into "$rootDir/build/libs/"
    rename { n -> "ChatMod-core.jar" }
}
